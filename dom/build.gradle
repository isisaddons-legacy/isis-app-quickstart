/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// this gradle script compiles and enhances the dom classes.
//
// The script is intended to be used in conjunction with IntelliJ IDEA, to remove the necessity to run the DataNucleus
// enhancer immediately before running the app, and therefore reduce the turn-around time.  The script is run as
// in the background as a daemon while editing/developing; whenever a change is made to any source code, gradle will
// automatically compile *and* enhance the code.
//
// To use, just type:
//
//      gradle -t --offline &
//
// It is also necessary to disable the IntelliJ's automatic building of the 'dom' project.  This is done using:
//
//      File > Settings > Build, Execution, Deployment > Compiler > Excludes
//
// and then exclude the .../dom/src/main/java directory
//
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

buildscript {
    repositories {
        maven { url "http://oss.jfrog.org/artifactory/oss-snapshot-local" }
        jcenter()
    }
    dependencies {
        classpath 'tangram:gradle-plugin:1.1.2'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.6'
}

apply plugin: 'java'
apply plugin: 'tangram.tools'
//apply plugin: 'io.franzbecker.gradle-lombok:1.6'

defaultTasks 'compileJava', 'copyClasses'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = (new XmlParser()).parse('pom.xml').parent.version.text()

repositories {
    mavenLocal()
    maven { url "http://oss.jfrog.org/artifactory/oss-snapshot-local" }
    jcenter()
}
dependencies {
    compile group: 'org.apache.isis.core', name: 'isis-core-applib', version: '1.13.0-SNAPSHOT'
    compile group: 'org.togglz', name: 'togglz-core', version: '2.1.0.Final';
    compile group: 'org.isisaddons.module.security', name: 'isis-module-security-dom', version: '1.13.0-SNAPSHOT';
}
lombok {
    version = "1.16.6"
}
task copyClasses << {
    copy {
        from 'build/classes/main'
        into 'target/classes'
    }
}
